apply plugin: 'java'
apply plugin: 'maven-publish'

ext.server = 'http://localhost:8081/artifactory'

repositories.maven { url "$server/repo" }

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.5'
  compile 'com.google.guava:guava:r08'
  testCompile 'junit:junit:4.+'
}

group = 'io.igorpopov'
version = '1.0-SNAPSHOT'

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }

    myBuildDotGradleScript(MavenPublication) {
      groupId 'io.igorpopov'
      artifactId 'build-script'
      version '3.14-SNAPSHOT'
      artifact file('build.gradle')
    }
  }
  repositories {
    maven {
      credentials {
        username 'admin'
        password 'password'
      }

      url chooseRepositoryFor(version)
    }
  }
}

def chooseRepositoryFor(version) {
  def isSnapshot = version.endsWith('-SNAPSHOT')
  def repositoryType = isSnapshot ? 'snapshot' : 'release'
  def repository = "libs-$repositoryType-local"
  return "$server/$repository"
}

